// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */



/**
* A w-bit counter consists of two main elements: 
* a regular w-bit register, and combinational logic. 
* The combinational logic is designed to 
* (a) compute the counting function, and 
* (b) put the counter in the right operating mode, 
* as mandated by the values of its three control bits. 
* Tip: Most of this logic was already built in chapter 2
*/

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=saved, out=inced);
    Mux16(a=saved, b=inced, sel=inc, out=muxedinc);
    Mux16(a=muxedinc, b=in, sel=load, out=muxedload);
    Mux16(a=muxedload, b=false, sel=reset, out=muxedreset);

    Register(in=muxedreset, load=true, out=out, out=saved);
}
